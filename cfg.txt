S' -> CODE
CODE -> VDECL CODE | FDECL CODE | ''
VDECL -> vtype id semi | vtype ASSIGN semi
ASSIGN -> id assign RHS
RHS -> EXPR | literal | character | boolstr
EXPR -> T_EXPR addsub EXPR | T_EXPR
T_EXPR -> F_EXPR multdiv T_EXPR | F_EXPR
F_EXPR -> lparen EXPR rparen | id | num
FDECL -> vtype id lparen ARG rparen lbrace BLOCK RETURN rbrace
ARG -> vtype id MOREARGS | ''
MOREARGS -> comma vtype id MOREARGS | ''
BLOCK -> STMT BLOCK | ''
STMT -> VDECL | ASSIGN semi | if lparen COND rparen lbrace BLOCK rbrace ELSE | while lparen COND rparen lbrace BLOCK rbrace
COND -> boolstr COND_T
COND_T -> comp boolstr COND_T | ''
ELSE -> else lbrace BLOCK rbrace | ''
RETURN -> return RHS semi